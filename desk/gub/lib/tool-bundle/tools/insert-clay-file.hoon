/<  tools  /lib/tools.hoon
::  insert-clay-file: insert or overwrite a file in Clay
::
!:
^-  tool:tools
|%
++  name  'insert_clay_file'
++  description
  ^~  %-  crip
  ;:  weld
    "Insert or overwrite a file in the Clay filesystem. "
    "Paths use slashes, not dots: /gen/hello/hoon (not /gen/hello.hoon). "
    "The last segment is the mark (e.g. /app/foo/hoon has mark %hoon). "
    "The desk must have a matching mark file in /mar/."
  ==
++  parameters
  ^-  (map @t parameter-def:tools)
  %-  ~(gas by *(map @t parameter-def:tools))
  :~  ['desk' [%string 'Target desk name (e.g. "base")']]
      ['path' [%string 'File path including mark (e.g. "/gen/hello/hoon")']]
      ['content' [%string 'File content to write']]
  ==
++  required  ~['desk' 'path' 'content']
++  handler
  ^-  tool-handler:tools
  =/  m  (fiber:fiber:nexus ,tool-result:tools)
  ^-  form:m
  ;<  st=tool-state:tools  bind:m  (get-state-as:io ,tool-state:tools)
  ?+  step.st  (pure:m [%error 'Unknown insert step'])
      %start
    ;<  err=(unit tang)  bind:m  (sleep-or-crud:tools (div ~s1 10))
    ?^  err
      =/  lines=wall  (zing (turn (flop u.err) |=(=tank (wash [0 80] tank))))
      (pure:m [%error (crip "Clay build failed:\0a{(of-wall:format lines)}")])
    =/  parsed=(each [@t @t @t] tang)
      %-  mule  |.
      %.  [%o args.st]
      %-  ot:dejs:format
      :~  ['desk' so:dejs:format]
          ['path' so:dejs:format]
          ['content' so:dejs:format]
      ==
    ?:  ?=(%| -.parsed)
      (pure:m [%error 'Missing or invalid required arguments (desk, path, content)'])
    =/  [desk=@t file-path=@t content=@t]  p.parsed
    =/  dek=@tas  (slav %tas desk)
    =/  pax=path  (stab file-path)
    ?~  pax
      (pure:m [%error 'Empty path'])
    =/  mark=@tas  (rear pax)
    ?.  ?=(?(%hoon %json %html %css %js %md %txt) mark)
      (pure:m [%error (crip "Unsupported mark: %{(trip mark)}. Use hoon, json, html, css, js, md, or txt.")])
    ::  every file goes to Clay as a mime; the desk's mark converts it
    =/  =mime  [/text/plain (as-octs:mimes:html content)]
    ;<  initial=cass:clay  bind:m  (clay-case:io dek)
    =/  write-data=json
      %-  pairs:enjs:format
      :~  ['initial-ud' (numb:enjs:format ud.initial)]
          ['desk' s+desk]
          ['file-path' s+file-path]
          ['logs' a+~]
      ==
    ;<  ~  bind:m
      (replace:io [tool.st args.st %inserting write-data ~])
    ;<  *  bind:m  (keep:io /dill/logs [%& %& /sys/dill %'logs.dill-told'] ~)
    ;<  now=@da  bind:m  get-time:io
    ;<  ~  bind:m
      (set-timer:io /commit-timeout (add now ~s30))
    ;<  ~  bind:m
      (clay-info:io dek [pax `mime]~)
    ;<  ~  bind:m  collect-logs:tools
    ;<  ~  bind:m  (drop:io /dill/logs [%& %& /sys/dill %'logs.dill-told'])
    ;<  st=tool-state:tools  bind:m  (get-state-as:io ,tool-state:tools)
    (finish-clay-write:tools args.st data.st)
      %inserting
    (finish-clay-write:tools args.st data.st)
  ==
--
